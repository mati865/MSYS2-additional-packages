
pkgname=autossh
pkgver=1.4e
pkgrel=1
pkgdesc="Automatically restart SSH sessions and tunnels"
arch=('i686' 'x86_64')
url="http://www.harding.motd.ca/autossh/"
license=('custom')
depends=('openssh')
source=("http://www.harding.motd.ca/autossh/$pkgname-$pkgver.tgz"
	"LICENSE")
sha256sums=('9e8e10a59d7619176f4b986e256f776097a364d1be012781ea52e08d04679156'
            '6c3bdba7b8f93c58fc5607397097f60676526f3baf82ef4f75c84db59c538db8')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}
package() {
  cd "${pkgname}-${pkgver}"
  install -D -m755 autossh $pkgdir/usr/bin/autossh
  install -D -m644 CHANGES $pkgdir/usr/share/doc/autossh/CHANGES
  install -D -m644 README $pkgdir/usr/share/doc/autossh/README
  install -D -m644 autossh.host $pkgdir/usr/share/autossh/examples/autossh.host
  install -D -m644 rscreen $pkgdir/usr/share/autossh/examples/rscreen
  install -D -m644 autossh.1 $pkgdir/usr/share/man/man1/autossh.1
  install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
